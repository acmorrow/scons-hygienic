Import('env')

env = env.Clone()

env.AppendUnique(
    LIBPATH=['$PREFIX/lib'],
    CPPPATH=['#/src'],
)

if env['PLATFORM'] == 'posix':
    env.AppendUnique(
        RPATH=[env.Literal('\\$$ORIGIN/../lib')],
        LINKFLAGS=['-Wl,-z,origin'],
    )
elif env['PLATFORM'] == 'darwin':
    env.AppendUnique(
        LINKFLAGS=[
            '-Wl,-rpath,@loader_path/../lib'
        ],
        SHLINKFLAGS=[
            "-Wl,-install_name,@loader_path/../lib/${TARGET.file}",
        ],
    )

if not 'MSVC_VERSION' in env:
    env.AppendUnique(
        CXXFLAGS=[
            '-std=c++11',
            '-fvisibility=hidden',
            '-fvisibility-inlines-hidden',
        ],
    )

env.SConscript(
    dirs=[
        'base',
        'derived',
    ],
    exports={
        'env' : env,
    },
)

configHeaders = env.File([
    'config.h'
])
installConfigHeaders = env.Install('$PREFIX/include', configHeaders)
env.Alias('install-dev', installConfigHeaders)

main = env.Program(
    target='main',
    source=[
        'main.cpp',
    ],
    LIBS=[
        'base',
        'derived',
    ]
)
